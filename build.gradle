buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.2.2'
		classpath 'me.tatarka:gradle-retrolambda:3.6.0'
	}
}

allprojects {
	repositories {
		jcenter()
	}
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

dependencies {
	provided 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}

def privateProperties = new Properties()

if (new File('private.properties').exists()) {
	privateProperties.load(new FileInputStream(file('private.properties')))
}

android {
	compileSdkVersion 26
	buildToolsVersion '25.0.2'

	sourceSets.main {
		manifest.srcFile 'AndroidManifest.xml'
		java.srcDirs = ['src', 'src-api', 'src-external']
		resources.srcDirs = ['src']
		aidl.srcDirs = ['src']
		renderscript.srcDirs = ['src']
		res.srcDirs = ['res']
		assets.srcDirs = ['assets']
		jni.srcDirs = []
		jniLibs.srcDir 'libs'
	}

	if (new File('keystore.properties').exists()) {
		def keystoreProperties = new Properties()
		keystoreProperties.load(new FileInputStream(file('keystore.properties')))

		signingConfigs {
			general {
				storeFile file(keystoreProperties['store.file'])
				storePassword keystoreProperties['store.password']
				keyAlias keystoreProperties['key.alias']
				keyPassword keystoreProperties['key.password']
			}
		}

		buildTypes.debug.signingConfig signingConfigs.general
		buildTypes.release.signingConfig signingConfigs.general
	}

	defaultConfig {
		def apiKeyGoogle = privateProperties['api.key.google']
		def apiKeySoundCloud = privateProperties['api.key.soundcloud']

		if (apiKeyGoogle == null) apiKeyGoogle = ''
		if (apiKeySoundCloud == null) apiKeySoundCloud = ''

		buildConfigField 'String', 'API_KEY_GOOGLE', '"' + apiKeyGoogle + '"'
		buildConfigField 'String', 'API_KEY_SOUNDCLOUD', '"' + apiKeySoundCloud + '"'

		buildConfigField 'long', 'BUILD_TIMESTAMP', System.currentTimeMillis() + 'L'
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	lintOptions {
		abortOnError false
		disable 'MissingTranslation', 'ValidFragment', 'ResourceType'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

def getNdkBuildCommand() {
	def ndkDir = System.env.ANDROID_NDK_HOME
	if (ndkDir == null && new File('local.properties').exists()) {
		Properties localProperties = new Properties()
		localProperties.load(new FileInputStream(file('local.properties')))
		ndkDir = localProperties.getProperty('ndk.dir')
	}
	def windows = org.gradle.internal.os.OperatingSystem.current().isWindows()
	return (ndkDir != null ? ndkDir + File.separator : '') + 'ndk-build' + (windows ? '.cmd' : '')
}

task ndkBuildCompile(type: Exec) {
	commandLine getNdkBuildCommand(), '-C', file('.').absolutePath
}

task ndkBuildClean(type: Exec) {
	commandLine getNdkBuildCommand(), 'clean', file('.').absolutePath
}

tasks.withType(JavaCompile) { compileTask ->
	compileTask.dependsOn ndkBuildCompile
}

clean.dependsOn ndkBuildClean